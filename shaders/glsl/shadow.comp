#version 460
#extension GL_EXT_nonuniform_qualifier: enable
#extension GL_GOOGLE_include_directive: enable
//#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_ray_query : enable



//--------------------------------------------------------------------------------------
// includes
//--------------------------------------------------------------------------------------
#include "core/core.glsl" //S0
#include "core/ray.glsl"
#include "core/ray_march_test.glsl"

//--------------------------------------------------------------------------------------
// inputs
//--------------------------------------------------------------------------------------

HYBRID_CORE_GBUFFER_SAMPLER //S1

layout(set = 1, binding = 3 ) writeonly buffer shadowStorage{
    float shadowMap[];
};

layout(set = 2, binding = 0, rgba16f) uniform image2D _testShadowMap;
layout(set = 2, binding = 1) uniform accelerationStructureEXT _tlas;
layout(set = 2, binding = 2) uniform modelData{
    mat4 _transform;
};

//layout(set = 2, binding = 2) readonly buffer vertexData{
//    hybrid_Vertex at[];
//}_vertex;
//
//layout(set = 2, binding = 3) readonly buffer indexData{
//    uint at[];
//}_index;

//--------------------------------------------------------------------------------------
// kernel
//--------------------------------------------------------------------------------------
layout(local_size_x = 32) in;
layout(local_size_y = 32) in;

//--------------------------------------------------------------------------------------
// Scene data TODO: Take it from CPU
//--------------------------------------------------------------------------------------

vec3 positions[3] = vec3[](
    vec3(0.0, -0.5,0.0),
    vec3(0.5, 0.5, 0.0),
    vec3(-0.5, 0.5,0.0)
);

//--------------------------------------------------------------------------------------
// programm
//--------------------------------------------------------------------------------------

//return hit distance if no hit returns -1
float triIntersect(in Ray r,in vec3 v0,in vec3 v1,in vec3 v2 )
{
    vec3 v1v0 = v1 - v0;
    vec3 v2v0 = v2 - v0;
    vec3 rov0 = r.origin - v0;
    vec3  n = cross( v1v0, v2v0 );
    vec3  q = cross( rov0, r.direction );
    float d = 1.0/dot( r.direction, n );
    float u = d*dot( -q, v2v0 );
    float v = d*dot(  q, v1v0 );
    float t = d*dot( -n, rov0 );
    if( u<0.0 || v<0.0 || (u+v)>1.0 ) t = MAX_FLOAT;
    if(t < 0.001) t= MAX_FLOAT;
    return t;
}


float intersectTLAS(in Ray r){

    rayQueryEXT rayQuery;
    float tmax = 1000.0;
    rayQueryInitializeEXT(
        rayQuery,
        _tlas,
        gl_RayFlagsTerminateOnFirstHitEXT,
        0xFF,
        r.origin,
        0.01,               //TODO: Standardize the ray bias
        r.direction,
        tmax
    );

    rayQueryProceedEXT(rayQuery);

    if(rayQueryGetIntersectionTypeEXT(rayQuery, true) != gl_RayQueryCommittedIntersectionTriangleEXT)
        return MAX_FLOAT;
    
    float t = rayQueryGetIntersectionTEXT(rayQuery, true);

    if(t<0) 
        return MAX_FLOAT;

    return t;
}

bool intersectMesh(in Ray r,in float d_light)
{
    //triangle intersection
    mat4 invTransform = inverse(_transform);
    Ray ray = transformRay(r,invTransform);

    float t = intersectTLAS(ray);
    t= min(d_light,t);
    if(t == d_light) return false;
    return true;
}

float intersectSdf(in Ray r,in float d_light)
{
    return softshadow(r.origin, r.direction, 0.1 , d_light, 0.2); //TODO: Decide the w value wrt. the light features
}

//returns 1 if there is a hit between light and rayOrigin
float hitWorld(in Ray r,in float d_light)
{
    if(intersectMesh(r,d_light)) return 0.1; //TODO: Add mesh distance field
    return intersectSdf(r,d_light); //soft shadows for sdf
}

void main() {
    
    ivec2 texCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 bounds = ivec2( _projectionParams.z, _projectionParams.w);
    uint idx = texCoords.y * bounds.x + texCoords.x; //used for shadow map

    if(any(greaterThanEqual(texCoords,bounds))) return;
   
    
    vec2 uv = vec2(texCoords)/vec2(bounds)+ (0.5*calculateUvSize());

    const vec4 gb1 = texture(gbuffer1, uv);
    const vec4 gb2 = texture(gbuffer2, uv); 

    shadowMap[idx] = 1.0; // no shadow
    float res;
    if(gb2.w == HYBRID_BACKGROUND_FLAG){
        res = 1.0;
        //return;
    }
    else{
        const vec3 worldPos = gb1.xyz;
        const vec3 worldNormal = gb2.xyz;
        vec3 rayOrigin = worldPos;
        for(uint i = 0; i < HYBRID_LIGHT_COUNT; ++i){
            
            vec3 rayDirection = normalize(_lights[i].position - worldPos);
            float rdl = dot(rayDirection,worldNormal);
            
            if(rdl < 0){
                continue;
            } 
    
            float t_light = distance(_lights[i].position , rayOrigin);
    
            Ray ray = createRay(rayOrigin, rayDirection);
            res = hitWorld(ray, t_light);
        }
        shadowMap[idx] = res;
    }
    
    vec4 shadowColor = vec4(vec3(res),1.0);
    imageStore(_testShadowMap,texCoords,shadowColor);
}